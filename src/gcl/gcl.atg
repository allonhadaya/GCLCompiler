$FPSX
COMPILER gcl

		static final boolean DIRECT = CodegenConstants.DIRECT;
		static final boolean INDIRECT = CodegenConstants.INDIRECT;
		IntegerType integerType = SemanticActions.INTEGER_TYPE;
		BooleanType booleanType = SemanticActions.BOOLEAN_TYPE;
		TypeDescriptor noType = SemanticActions.NO_TYPE;

/*==========================================================*/
CHARACTERS 
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
  digit = "0123456789".
  eol = '\r'.
  nl = '\n'.
  tab = '\t'.
  underscore = "_" .
  singleQuotedStringCharacters = ANY - "'" - eol - nl - '\0'. // '\0' represents
  doubleQuotedStringCharacters = ANY - '"' - eol - nl - '\0'. // the null character

TOKENS
  identifier  = letter { letter | digit | underscore } .
  numeral = digit {digit}.
  gclString = "'" { singleQuotedStringCharacters } "'" 
  				 | '"' { doubleQuotedStringCharacters } '"'.

PRAGMAS 
	option1 = "$"("c"|"C")("+"|"-").
		(. CompilerOptions.listCode = la.val.charAt(2) == '+'; .)
	option3 = "$"("o"|"O")("+"|"-").
		(. CompilerOptions.optimize = la.val.charAt(2) == '+'; .)
	option5 = "$"("s"|"S")"+". (. SymbolTable.dumpAll(); .)
	option6 = "$"("s"|"S")"-".  /* ignore */
	option7 = "$"("m"|"M")("+"|"-").
		(. CompilerOptions.showMessages = la.val.charAt(2) == '+'; .)
	option9 = "$"("r"|"R")"+". (. CompilerOptions.printAllocatedRegisters(); .)
	option10 = "$"("r"|"R")"-".  /* ignore */

COMMENTS FROM "{" TO "}" NESTED
COMMENTS FROM "--" TO eol
COMMENTS FROM "--" TO nl   // Added for running on OSX

IGNORE eol + tab + nl

/*==========================================================*/
PRODUCTIONS

gcl
=
	(. semantic.startCode();  SymbolTable scope = SymbolTable.currentScope(); .)
	SYNC module<scope> 
	{ (. scope = scope.openScope(true); .) SYNC module<scope> } 
	(. semantic.finishCode(); .) .
/*----------------------------------------------------------*/
module<SymbolTable scope> 
= 
	(. String result; .)
	"module" validIdentifier<^result>
		definitionPart<scope>
	[ "private" (. SymbolTable privateScope = scope.openScope(false); .)
		block<privateScope>
	] "." . 
/*----------------------------------------------------------*/
block<SymbolTable scope>
=
     definitionPart<scope> SYNC "begin"  statementPart<scope> "end".
/*----------------------------------------------------------*/
definitionPart<SymbolTable scope>   =    {SYNC definition<scope> WEAK ";" }.
/*----------------------------------------------------------*/
statementPart<SymbolTable scope> 
= 
	SYNC statement<scope> WEAK ";" { SYNC statement<scope> WEAK ";"}.
/*----------------------------------------------------------*/
definition<SymbolTable scope>  
=  
	variableDefinition<scope, ParameterKind.NOT_PARAM> |
	constantDefinition<scope> |
	typeDefinition<scope>.
/*----------------------------------------------------------*/
constantDefinition<SymbolTable scope>
	(. String result; Identifier id; Expression exp; .)
=
	"constant" validIdentifier<^result>
	(. id = new Identifier(result); .)
	"=" expression<^exp,scope>
	(. semantic.declareConstant(scope, id, exp.expectConstantExpression(err)); .) .
/*----------------------------------------------------------*/
variableDefinition<SymbolTable scope, ParameterKind kindOfParam> 
	(. String result; TypeDescriptor type; Identifier id; .)
=
	type<^type, scope> validIdentifier<^result>
		(. 	id = new Identifier(result);
			semantic.declareVariable(scope, type, id, kindOfParam);
		.)
	{	"," validIdentifier<^result>
		(. 	id = new Identifier(result);
			semantic.declareVariable(scope, type, id, kindOfParam);
		.)
	}.
/*----------------------------------------------------------*/
arrayType<^TypeDescriptor result, TypeDescriptor componentType, SymbolTable scope>
=
	(. SemanticItem subscriptType; java.util.Stack<RangeType> subscripts = new java.util.Stack<RangeType>(); .)
	"array" "[" qualifiedIdentifier<^subscriptType, scope> (. subscripts.push(subscriptType.expectTypeDescriptor(err).expectRangeType(err)); .) "]"
		  { "[" qualifiedIdentifier<^subscriptType, scope> (. subscripts.push(subscriptType.expectTypeDescriptor(err).expectRangeType(err)); .) "]" }
	(. result = semantic.declareArrayType(subscripts, componentType); .).
/*----------------------------------------------------------*/
rangeType<^TypeDescriptor result, TypeDescriptor baseType, SymbolTable scope>
=
	(. Expression lowerBound; Expression upperBound; .)
	"range" "[" expression<^lowerBound, scope> ".." expression<^upperBound, scope> "]"
	(. result = semantic.declareRangeType(lowerBound, upperBound, baseType); .).
/*----------------------------------------------------------*/
typeDefinition<SymbolTable scope>
	(. TypeDescriptor type; String result; .)
=
	"typedefinition" type<^type, scope> validIdentifier<^result>
	(. semantic.declareTypeDefinition(scope, type, new Identifier(result)); .).
/*----------------------------------------------------------*/
type<^TypeDescriptor result, SymbolTable scope>	(. result = noType; .)
=
	typeSymbol<^result, scope> [ arrayType<^result, result, scope> | rangeType<^result, result, scope> ]  |  tupleType<^result, scope>.
/*----------------------------------------------------------*/
typeSymbol<^TypeDescriptor result, SymbolTable scope> (. result = null; SemanticItem typeItem = null; .)
=
	"integer" (. result = integerType; .)
	| "Boolean" (. result = booleanType; .)
	| qualifiedIdentifier<^typeItem, scope> (. result = typeItem.expectTypeDescriptor(err); .) .
/*----------------------------------------------------------*/
tupleType<^TupleType result, SymbolTable scope>   (. TypeDescriptor type; Identifier id; String name; .)
=
	(. TypeList carrier = new TypeList(); .)
	"tuple" "[" typeSymbol<^type,scope> validIdentifier<^name> 
		(.id = new Identifier(name); carrier.enter(type, id);.)
		{  "," typeSymbol<^type,scope> identifier 
		(.id = new Identifier(name); carrier.enter(type, id);.) 
		} 
	"]"
     (. result = new TupleType(carrier); .) .
/*----------------------------------------------------------*/
statement<SymbolTable scope>
=
	emptyStatement | readStatement<scope>  | writeStatement<scope> 
	| assignStatement<scope> | ifStatement<scope> | doStatement<scope>
	| forStatement<scope>.
/*----------------------------------------------------------*/
emptyStatement     = "skip".
/*----------------------------------------------------------*/
readStatement<SymbolTable scope>      (. Expression exp; .)
=
	"read" variableAccessEtc<^exp, scope>  (. semantic.readVariable(exp); .)
		{ ","  variableAccessEtc<^exp, scope>  (. semantic.readVariable(exp); .) } .
/*----------------------------------------------------------*/
writeStatement<SymbolTable scope> 
=
	"write" writeItem<scope> {"," writeItem<scope> }
		(. semantic.genEol(); .).
/*----------------------------------------------------------*/
writeItem<SymbolTable scope> (. Expression exp; .)
=
	expression<^exp, scope>  (. semantic.writeExpression(exp); .)		
	| gclString (. semantic.writeString(new StringConstant(currentToken().spelling())); .).
/*----------------------------------------------------------*/
assignStatement<SymbolTable scope>
		(. AssignRecord expressions = new AssignRecord(); Expression exp; .)
=
	variableAccessEtc<^exp, scope> (. expressions.left(exp.expectVariableExpression(err)); .)
		{ "," variableAccessEtc<^exp, scope> (. expressions.left(exp.expectVariableExpression(err)); .) }
		":="
		expression<^exp, scope> (. expressions.right(exp); .)
		{ "," expression<^exp, scope> (. expressions.right(exp); .) }
		(. semantic.parallelAssign(expressions); .).
/*----------------------------------------------------------*/
doStatement<SymbolTable scope>  (. GCRecord doRecord; .)
=
	"do" (. doRecord = semantic.startDo(); .)  guardedCommandList<scope, doRecord > 
	"od" .
/*----------------------------------------------------------*/
forStatement<SymbolTable scope>  (. ForRecord forRecord; Expression control; .)
=
	"forall" variableAccessEtc<^control, scope> (. forRecord = semantic.startForall(control, err); .) "->"
	statementPart<scope> 
	"llarof" (. semantic.endForall(forRecord, err); .).
/*----------------------------------------------------------*/
ifStatement<SymbolTable scope>  (. GCRecord ifRecord; .)
=
	"if" (. ifRecord = semantic.startIf(); .)  guardedCommandList<scope, ifRecord > 
	"fi" (. semantic.endIf(ifRecord); .).
/*----------------------------------------------------------*/
guardedCommandList<SymbolTable scope, GCRecord ifRecord>
=
	guardedCommand<scope, ifRecord> { "[]" guardedCommand<scope, ifRecord> }.
/*----------------------------------------------------------*/
guardedCommand<SymbolTable scope, GCRecord  ifRecord >  (.  Expression expr; .)
=
	expression<^expr, scope> (. semantic.ifTest(expr, ifRecord); .)  "->"
	statementPart<scope>  (. semantic.elseIf(ifRecord); .).
/*----------------------------------------------------------*/
expression<^Expression left, SymbolTable scope>
(. Expression right; .)
=
	andExpression<^left, scope> {"|" andExpression<^right, scope> (. left = semantic.orExpression(left, right); .)} .
/*----------------------------------------------------------*/
andExpression<^Expression left, SymbolTable scope>
(. Expression right; .)
=
	relationalExpr<^left, scope> {"&" relationalExpr<^right, scope> (. left = semantic.andExpression(left, right); .)} .
/*----------------------------------------------------------*/
relationalExpr<^Expression left, SymbolTable scope>
	(. Expression right; RelationalOperator op; .)
=
	simpleExpr<^left, scope>
		[ relationalOperator<^op> simpleExpr<^right, scope>
		(. left = semantic.compareExpression(left, op, right); .)
		].
/*----------------------------------------------------------*/
simpleExpr<^Expression left, SymbolTable scope>
	(. Expression right; AddOperator op; left = null; .)
=
	(	["+"]term<^ left, scope>
		|  "-" term<^ left, scope>  
					(. left = semantic.negateExpression(left); .)
		|  "~" term <^ left, scope>
					(. left = semantic.negateBooleanExpression(left); .)
	)
	{  	addOperator<^op>  term<^right, scope>
			(. left = semantic.addExpression(left, op, right); .)
	}.
/*----------------------------------------------------------*/
term<^Expression left, SymbolTable scope>
	(. Expression right; MultiplyOperator op; left = null; .)
=
	factor<^left, scope> { multiplyOperator<^op>  factor<^right, scope>
	(. left = semantic.multiplyExpression(left, op, right); .) } .

/*----------------------------------------------------------*/
factor<^Expression result, SymbolTable scope>  (. result = null; .)
=
	   variableAccessEtc<^result, scope>
	|numeral
	  (. result = new ConstantExpression (integerType, Integer.parseInt(currentToken().spelling()));
	  .)
	|booleanConstant
	  (. result = new ConstantExpression (booleanType, (Boolean.parseBoolean(currentToken().spelling())) ? 1 : 0);
	  .)
	| "(" expression<^result, scope> ")"
	| (. Expression exp;
		 ExpressionList tupleFields = new ExpressionList();
	  .)
		"[" expression<^exp, scope>
			(. tupleFields.enter(exp); .)
		 { "," expression<^exp, scope>
		 	(. tupleFields.enter(exp); .)
		 }
		"]"  (. result = semantic.buildTuple(tupleFields); .).
/*----------------------------------------------------------*/
relationalOperator<^RelationalOperator op>  (. op = null; .)
=
	"="  (. op = RelationalOperator.EQUAL; .)
	| "#" (. op = RelationalOperator.NOT_EQUAL; .)
	| ">" (. op = RelationalOperator.GREATER; .)
	| ">=" (. op = RelationalOperator.GREATER_OR_EQUAL; .)
	| "<" (. op = RelationalOperator.LESS; .)
	| "<=" (. op = RelationalOperator.LESS_OR_EQUAL; .) .
	
/*----------------------------------------------------------*/
addOperator<^AddOperator op > (. op = null; .)
 =
 	"+"  (. op = AddOperator.PLUS; .)
 	| "-" (. op = AddOperator.MINUS; .).
/*----------------------------------------------------------*/
multiplyOperator<^MultiplyOperator op > 	(. op = null; .)
=
	"*"   (. op = MultiplyOperator.TIMES; .)
	| "/"  (. op = MultiplyOperator.DIVIDE; .)
	| "\\" (. op = MultiplyOperator.MODULO; .).
/*----------------------------------------------------------*/
qualifiedIdentifier<^SemanticItem result, SymbolTable scope> 
=
identifier 
	(. String value = currentToken().spelling();
       Identifier id = new Identifier(value);
	   result = semantic.semanticValue(scope, id);
	.)
["." identifier
	(. String value2 = currentToken().spelling();
       Identifier qualified = new Identifier(value2);
	   result = semantic.semanticValue(scope, qualified);
	.)
].
/*----------------------------------------------------------*/
validIdentifier<^String result> 
=
identifier 
	(. result = currentToken().spelling();
	   semantic.checkIdentifierSpelling(result);
	.).
/*----------------------------------------------------------*/
variableAccessEtc<^Expression result, SymbolTable scope>  
    (. SemanticItem workValue; .)   
=
	qualifiedIdentifier<^workValue, scope> subsAndCompons<^result, workValue, scope>.
/*----------------------------------------------------------*/
subsAndCompons<^Expression result, SemanticItem identifier, SymbolTable scope>
=
	(. result = identifier.expectExpression(err); Expression subscript; SemanticItem component; .)
	{  "[" expression<^subscript, scope> "]" (. result = semantic.subscript(result.expectVariableExpression(err), subscript); .)
	 | "@" qualifiedIdentifier<^component, scope> (. /*result = semantic.tupleComponent(result, component.expectTypeDescriptor(err).expectTupleType(err));*/ .) }.
/*----------------------------------------------------------*/
booleanConstant
=
"true" | "false".
/*----------------------------------------------------------*/

END gcl.